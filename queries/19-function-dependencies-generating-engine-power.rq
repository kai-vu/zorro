# Which functions depend directly or indirectly on function X?
# And which functions does function X depend on, directly or indirectly?

PREFIX : <https://w3id.org/zorro#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?relationship ?dependency ?dependencyLabel
WHERE {
    BIND(:GeneratingPower AS ?focusFunction)
    ?focusFunction rdf:type :Function .
    {
        BIND("dependedOnBy" AS ?relationship)
        ?dependency :dependsOn+ ?focusFunction ;
                    rdf:type :Function ;
                    rdfs:label ?dependencyLabel .
    }
    UNION
    {
        BIND("dependsOn" AS ?relationship)
        ?focusFunction :dependsOn+ ?dependency .
        ?dependency rdf:type :Function ;
                    rdfs:label ?dependencyLabel .
    }
}
ORDER BY ?relationship ?dependencyLabel
